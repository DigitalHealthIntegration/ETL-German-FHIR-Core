plugins {
    id "org.springframework.boot" version "2.7.5"
    id "io.spring.dependency-management" version "1.1.0"
    id "java"
    id "jacoco"
    id "io.freefair.lombok" version "6.5.1"
    id "com.diffplug.spotless" version "6.11.0"
}

group = "org.miracum.etl"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"
targetCompatibility = "17"

repositories {
    mavenCentral()
}

ext {
    set("springCloudVersion", "2021.0.4")
    set('log4j2.version', "2.17.1")
    set('hapiVersion', "6.1.3")
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-batch"
    implementation "org.springframework.cloud:spring-cloud-starter-task"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.eclipse.persistence:javax.persistence:2.2.1"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "com.github.ben-manes.caffeine:caffeine:3.1.1"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:${hapiVersion}"
    constraints {
        implementation('org.apache.commons:commons-text:1.10.0') {
            because 'CVE-2022-42889'
        }
    }
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:${hapiVersion}"
    implementation "ca.uhn.hapi.fhir:org.hl7.fhir.r4:5.6.46"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:${hapiVersion}"
    implementation "com.h2database:h2:2.1.214"
    implementation "org.postgresql:postgresql:42.5.0"
    implementation "io.micrometer:micrometer-registry-prometheus:1.9.0"
    implementation "io.micrometer:micrometer-core:1.9.0"
    implementation "io.prometheus:simpleclient_pushgateway:0.15.0"
    implementation "org.fhir:ucum:1.0.3"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1"
    implementation "io.jaegertracing:jaeger-client:1.8.0"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "org.springframework.batch:spring-batch-test"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    enabled = false
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = false
        csv.required = true
    }
}

spotless {
    java {
        importOrder() // standard import order
        removeUnusedImports()
        googleJavaFormat()
    }
}
